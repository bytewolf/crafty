#!/usr/bin/env perl

use strict;
use warnings;

my $root;
use Config;

BEGIN {
    use File::Basename ();
    use File::Spec     ();
    $root = File::Spec->catfile(File::Basename::dirname(__FILE__), '..');

    unshift @INC, "$root/perl5/lib/perl5"                   if -d "$root/perl5";
    unshift @INC, "$root/perl5/lib/perl5/$Config{archname}" if -d "$root/perl5";
    unshift @INC, "$root/lib";
}

use Cwd qw(abs_path);
use Plack::Runner;
use Getopt::Long;
use Crafty;
use Crafty::Log;
use Crafty::Config;
use Crafty::PubSub;

my $opt_base   = 'data';
my $opt_config = 'data/config.yml';
my $opt_listen;
my $opt_verbose;
GetOptions(
    'config=s' => \$opt_config,
    'base=s'   => \$opt_base,
    'listen=s' => \$opt_listen,
    'verbose'  => \$opt_verbose,
) or die("Error in command line arguments\n");

Crafty::Log->init(verbose => 1) if $opt_verbose;

my $config = Crafty::Config->new(root => $root, base => abs_path($opt_base));
$config->load($opt_config);

$opt_listen //= $config->config->{listen} // '0.0.0.0:5000';
$config->config->{listen} //= $opt_listen;

Crafty::PubSub->instance->listen($opt_base . '/crafty.sock');

my $app = Crafty->new(config => $config, root => $root);

my $w;
$w = AE::signal HUP => sub {
    warn "Exiting gracefully (waiting for everything to finish)...\n";

    sleep 2;
    undef $w;

    kill 'QUIT', $$;

    sleep 5;

    kill 'TERM', $$;
};

my $psgi = $app->to_psgi;

warn "Listening on $opt_listen ($$)\n";

my $runner = Plack::Runner->new;
$runner->parse_options(
    '-s' => 'Twiggy',
    '-E' => 'production',
    '-l' => $opt_listen
);
$runner->run($psgi);
